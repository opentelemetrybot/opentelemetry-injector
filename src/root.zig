// Copyright The OpenTelemetry Authors
// SPDX-License-Identifier: Apache-2.0

const builtin = @import("builtin");
const std = @import("std");

const config = @import("config.zig");
const dotnet = @import("dotnet.zig");
const libc = @import("libc.zig");
const jvm = @import("jvm.zig");
const nodejs = @import("nodejs.zig");
const print = @import("print.zig");
const proc_self_environ_values = @import("proc_self_environ_values.zig");
const proc_self_environ_parser = @import("proc_self_environ_parser.zig");
const python = @import("python.zig");
const ruby = @import("ruby.zig");
const res_attrs = @import("resource_attributes.zig");
const types = @import("types.zig");
const pattern_matcher = @import("patterns_matcher.zig");
const args_parser = @import("args_parser.zig");

const init_section_name = switch (builtin.target.os.tag) {
    .linux => ".init_array",
    // Note: the injector does not support any OS besides Linux, this case is only here to support running Zig unit
    // tests directly on Darwin.
    .macos => "__DATA,__mod_init_func",
    else => {
        error.OsNotSupported;
    },
};

export const init_array: [1]*const fn () callconv(.c) void linksection(init_section_name) = .{&initEnviron};

fn initEnviron() callconv(.c) void {
    const allocator = std.heap.page_allocator;

    proc_self_environ_parser.initFromProcSelfEnviron() catch |err| {
        // If we fail to read the log level, we continue processing, using the default log level.
        print.printError("failed to read log level from environment: {}", .{err});
        print.printError("using default log level {}", .{print.getLogLevel()});
    };

    if (proc_self_environ_values.getOtelInjectorDisabled()) {
        print.printInfo(
            "Injector has been explicitly disabled via {s}, no environment variables will be modified.",
            .{proc_self_environ_parser.otel_injector_disabled_env_var_name},
        );
        return;
    }

    // Read config and evaluate allow/deny before libc detection, so that filtered-out
    // processes never attempt libc detection and never emit libc-related warnings.
    // config.readConfiguration uses proc_self_environ_parser.getenv internally, which
    // reads /proc/self/environ directly without requiring libc.
    var configuration = config.readConfiguration(allocator, proc_self_environ_parser.getenv);
    defer configuration.deinit(allocator);

    if (!evaluateAllowDeny(allocator, configuration)) {
        return;
    }

    const libc_info = libc.getLibCInfo(allocator) catch |err| {
        if (err == error.UnknownLibCFlavor) {
            print.printError("no libc found: {}", .{err});
        } else {
            print.printError("failed to identify libc: {}", .{err});
        }
        return;
    };
    dotnet.setLibcInfo(libc_info);
    python.setLibcInfo(libc_info);
    ruby.setLibcInfo(libc_info);
    res_attrs.setLibcInfo(libc_info);

    const maybe_modified_resource_attributes = res_attrs.getModifiedOtelResourceAttributesValue(allocator) catch |err| {
        print.printError("cannot calculate modified OTEL_RESOURCE_ATTRIBUTES: {}", .{err});
        return;
    };

    if (maybe_modified_resource_attributes) |modified_resource_attributes| {
        // Note: getModifiedOtelResourceAttributesValue returns a sentinel-terminated slices, which can be coerced
        // automatically into the sentinel-terminated many pointer which is required by setenv.
        const setenv_res =
            libc_info.setenv_fn_ptr(
                res_attrs.otel_resource_attributes_env_var_name,
                modified_resource_attributes,
                true,
            );
        if (setenv_res == 0) {
            print.printDebug(
                "setting \"{s}\"=\"{s}\"",
                .{ res_attrs.otel_resource_attributes_env_var_name, modified_resource_attributes },
            );
        } else {
            print.printError(
                "failed to set \"{s}\"=\"{s}\", setenv returned: {d}",
                .{ res_attrs.otel_resource_attributes_env_var_name, modified_resource_attributes, setenv_res },
            );
        }
    }

    modifyEnvironmentVariable(
        allocator,
        libc_info,
        nodejs.node_options_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        jvm.java_tool_options_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        python.pythonpath_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        ruby.rubyopt_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        ruby.ruby_additional_gem_path_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.coreclr_enable_profiling_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.coreclr_profiler_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.coreclr_profiler_path_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.dotnet_additional_deps_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.dotnet_shared_store_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.dotnet_startup_hooks_env_var_name,
        configuration,
    );
    modifyEnvironmentVariable(
        allocator,
        libc_info,
        dotnet.otel_dotnet_auto_home_env_var_name,
        configuration,
    );

    setCustomEnvironmentVariables(
        allocator,
        libc_info,
        configuration.all_auto_instrumentation_agents_env_vars,
    );

    print.printInfo("environment injection finished", .{});
}

fn libcGetenv(getenv_fn: types.GetenvFnPtr, name: [:0]const u8) ?[:0]const u8 {
    return std.mem.span(getenv_fn(name) orelse return null);
}

fn evaluateAllowDeny(allocator: std.mem.Allocator, configuration: config.InjectorConfiguration) bool {
    const exe_path = getExecutablePath(allocator) catch {
        // Skip allow-deny evaluation if getting the executable path has failed. The error has already been logged in
        // getExecutablePath.
        return true;
    };
    defer allocator.free(exe_path);

    const args = getCommandLineArgs(allocator) catch {
        // Skip allow-deny evaluation if getting the arguments has failed. The error has already been logged in
        // getCommandLineArgs.
        return true;
    };
    defer {
        for (args) |arg| allocator.free(arg);
        allocator.free(args);
    }

    const allow = pattern_matcher.evaluateAllow(exe_path, args, configuration.include_paths, configuration.include_args);
    const deny = pattern_matcher.evaluateDeny(exe_path, args, configuration.exclude_paths, configuration.exclude_args);

    if (!allow or deny) {
        print.printDebug("executable with path {s} ignored. allow={any}, deny={any}", .{ exe_path, allow, deny });
        if (print.isDebug()) {
            if (configuration.include_paths.len > 0) {
                print.printDebug("  include_paths:", .{});
                for (configuration.include_paths) |pattern| {
                    print.printDebug("    - {s}", .{pattern});
                }
            }
            if (configuration.include_args.len > 0) {
                print.printDebug("  include_arguments:", .{});
                for (configuration.include_args) |pattern| {
                    print.printDebug("    - {s}", .{pattern});
                }
            }
            if (configuration.exclude_paths.len > 0) {
                print.printDebug("  exclude_paths:", .{});
                for (configuration.exclude_paths) |pattern| {
                    print.printDebug("    - {s}", .{pattern});
                }
            }
            if (configuration.exclude_args.len > 0) {
                print.printDebug("  exclude_arguments:", .{});
                for (configuration.exclude_args) |pattern| {
                    print.printDebug("    - {s}", .{pattern});
                }
            }
        }
        return false;
    }
    return true;
}

fn getCommandLineArgs(allocator: std.mem.Allocator) ![]const []const u8 {
    // Get command line arguments.
    // Dynamically injected libraries don't get std.process.argsAlloc populated and
    // neither does std.os.argv. We read using the /proc/{pid}/cmdline.
    const cmdline_args = args_parser.cmdLineForPID(allocator) catch |err| {
        print.printDebug("failed to get executable arguments: {any}", .{err});
        return err;
    };

    if (print.isDebug()) {
        for (cmdline_args, 0..) |arg, i| {
            print.printDebug("arg[{d}]: {s}", .{ i, arg });
        }
    }

    return cmdline_args;
}

fn getExecutablePath(allocator: std.mem.Allocator) ![]u8 {
    // Get the program full executable path
    const executable_path = std.fs.selfExePathAlloc(allocator) catch |err| {
        print.printDebug("failed to get executable path: {any}", .{err});
        return err;
    };

    print.printDebug("executable: {s}", .{executable_path});

    return executable_path;
}

fn modifyEnvironmentVariable(
    allocator: std.mem.Allocator,
    lci: types.LibCInfo,
    name: [:0]const u8,
    configuration: config.InjectorConfiguration,
) void {
    if (getEnvValue(allocator, lci, name, configuration)) |value| {
        // Note: We must *not* free/deallocate the return value of getEnvValue after handing it over to setenv, or we
        // may cause a USE_AFTER_FREE memory corruption in the parent process.
        // Note: getEnvValue returns a sentinel-terminated slices, which can be coerced automatically into the
        // sentinel-terminated many pointer which is required by setenv.
        const setenv_res = lci.setenv_fn_ptr(name, value, true);
        if (setenv_res == 0) {
            print.printDebug(
                "setting \"{s}\"=\"{s}\"",
                .{ name, value },
            );
        } else {
            print.printError(
                "failed to set \"{s}\"=\"{s}\", setenv returned: {d}",
                .{ name, value, setenv_res },
            );
        }
    }
}

fn getEnvValue(
    allocator: std.mem.Allocator,
    lci: types.LibCInfo,
    name: [:0]const u8,
    configuration: config.InjectorConfiguration,
) ?[:0]const u8 {
    const original_value = libcGetenv(lci.getenv_fn_ptr, name);
    if (std.mem.eql(u8, name, jvm.java_tool_options_env_var_name)) {
        return jvm.checkOTelJavaAgentJarAndGetModifiedJavaToolOptionsValue(
            allocator,
            original_value,
            configuration,
        );
    } else if (std.mem.eql(u8, name, nodejs.node_options_env_var_name)) {
        return nodejs.checkNodeJsAutoInstrumentationAgentAndGetModifiedNodeOptionsValue(
            allocator,
            original_value,
            configuration,
        );
    } else if (std.mem.eql(u8, name, python.pythonpath_env_var_name)) {
        return python.checkPythonAutoInstrumentationAgentAndGetModifiedPythonpathValue(
            allocator,
            original_value,
            configuration,
        );
    } else if (std.mem.eql(u8, name, ruby.rubyopt_env_var_name)) {
        // The two Ruby env vars are a coupled pair: our RUBYOPT entry file requires its bundled OpenTelemetry
        // dependencies from OTEL_RUBY_ADDITIONAL_GEM_PATH. If the user has pre-set the gem path, we respect
        // their value (see below) -- so we must also skip RUBYOPT here to avoid a hybrid boot where our entry
        // file loads but its deps resolve against the user's path (LoadError on every Ruby process).
        if (libcGetenv(lci.getenv_fn_ptr, ruby.ruby_additional_gem_path_env_var_name) != null) {
            print.printInfo("Skipping the injection of the Ruby OpenTelemetry auto-instrumentation because \"{s}\" is already set to a user-provided value.", .{ruby.ruby_additional_gem_path_env_var_name});
            return null;
        }
        return ruby.checkRubyAutoInstrumentationAgentAndGetModifiedRubyoptValue(
            allocator,
            original_value,
            configuration,
        );
    } else if (std.mem.eql(u8, name, ruby.ruby_additional_gem_path_env_var_name)) {
        // Respect a user-provided value; only set it ourselves when Ruby injection is active. The RUBYOPT branch
        // above mirrors this decision by standing down when the user has already set this variable.
        if (original_value != null) return null;
        return ruby.getRubyAdditionalGemPath(allocator, configuration);
    } else if (std.mem.eql(u8, name, dotnet.coreclr_enable_profiling_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            return v.coreclr_enable_profiling;
        }
    } else if (std.mem.eql(u8, name, dotnet.coreclr_profiler_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            return v.coreclr_profiler;
        }
    } else if (std.mem.eql(u8, name, dotnet.coreclr_profiler_path_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            return v.coreclr_profiler_path;
        }
    } else if (std.mem.eql(u8, name, dotnet.dotnet_additional_deps_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            if (v.additional_deps) |ad| {
                return ad;
            } else {
                // No additional_deps available, return null to skip injection of DOTNET_ADDITIONAL_DEPS.
                return null;
            }
        }
    } else if (std.mem.eql(u8, name, dotnet.dotnet_shared_store_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            if (v.shared_store) |ss| {
                return ss;
            } else {
                // No shared_store available, return null to skip injection of DOTNET_SHARED_STORE.
                return null;
            }
        }
    } else if (std.mem.eql(u8, name, dotnet.dotnet_startup_hooks_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            return v.startup_hooks;
        }
    } else if (std.mem.eql(u8, name, dotnet.otel_dotnet_auto_home_env_var_name)) {
        if (dotnet.getDotnetValues(allocator, configuration)) |v| {
            return v.otel_auto_home;
        }
    }

    return null;
}

fn setCustomEnvironmentVariables(
    allocator: std.mem.Allocator,
    lci: types.LibCInfo,
    custom_env_vars: std.StringHashMap([]u8),
) void {
    if (custom_env_vars.count() == 0) {
        return;
    }
    var env_var_iterator = custom_env_vars.iterator();
    while (env_var_iterator.next()) |env_var| {
        const name = allocator.dupeZ(u8, env_var.key_ptr.*) catch |err| {
            print.printError(
                "error allocating memory for name when setting custom environment variable \"{s}\"=\"{s}\" (remaining custom environment variables will be skipped): {}",
                .{
                    env_var.key_ptr.*,
                    env_var.value_ptr.*,
                    err,
                },
            );
            return;
        };
        const value = allocator.dupeZ(u8, env_var.value_ptr.*) catch |err| {
            print.printError(
                "error allocating memory for value when setting custom environment variable \"{s}\"=\"{s}\" (remaining custom environment variables will be skipped): {}",
                .{
                    env_var.key_ptr.*,
                    env_var.value_ptr.*,
                    err,
                },
            );
            return;
        };
        const setenv_res = lci.setenv_fn_ptr(name, value, true);
        if (setenv_res == 0) {
            print.printDebug("setting \"{s}\"=\"{s}\"", .{ name, value });
        } else {
            print.printError(
                "failed to set \"{s}\"=\"{s}\", setenv returned: {d}",
                .{ name, value, setenv_res },
            );
        }
    }
}
